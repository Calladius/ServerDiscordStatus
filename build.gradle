plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'com.sds.serverdiscord'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/releases/' }
}

ext {
    paperVersion = '1.21.8-R0.1-SNAPSHOT'
    jdaVersion = '5.0.0'
    placeholderApiVersion = '2.11.7'
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperVersion}"
    compileOnly "me.clip:placeholderapi:${placeholderApiVersion}"

    implementation("net.dv8tion:JDA:${jdaVersion}") {
        exclude module: 'opus-java'
    }
}

def targetJavaVersion = 21

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

runServer {
    minecraftVersion("1.21.8")
    jvmArgs = ['-Xmx2G']
}

shadowJar {
    archiveBaseName.set('ServerDiscordStatus')
    archiveClassifier.set('')

    // Relocate JDA и зависимости для избежания конфликтов
    relocate 'net.dv8tion.jda', 'com.sds.serverdiscord.libs.jda'
    relocate 'com.neovisionaries.ws', 'com.sds.serverdiscord.libs.ws'
    relocate 'okhttp3', 'com.sds.serverdiscord.libs.okhttp3'
    relocate 'okio', 'com.sds.serverdiscord.libs.okio'

    mergeServiceFiles()
}

build {
    dependsOn shadowJar
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand([version: project.version])
    }
}